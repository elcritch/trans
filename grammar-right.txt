Program    : block


block      : '{' decls stmts '}'


decls      : decl decls
           |  


decl       : type id ';'


type       : basic type_1

type_1     : '[' num ']' type_1
           |  

basic      : 'int'
           | 'float'


stmts      : stmt stmts
           |  


stmt       : loc '=' bool ';'
           | 'if' '(' bool ')' stmt
           | 'if' '(' bool ')' stmt 'else' stmt
           | 'while' '(' bool ')' stmt
           | 'do' stmt 'while' '(' bool ')' ';'
           | 'break' ';'
           | block
           | 'read' loc ';'
           | 'write' bool ';'


loc        : id loc_1

loc_1      : '[' bool ']' loc_1
           |  

bool       : join bool_1

bool_1     : '||' join bool_1
           |  

join       : equality join_1

join_1     : '&&' equality join_1
           |  

equality   : rel equality_1

equality_1 : '==' rel equality_1
           | '!=' rel equality_1
           |  

rel        : expr '<' expr
           | expr '<=' expr
           | expr '>=' expr
           | expr '>' expr
           | expr


expr       : term expr_1

expr_1     : '+' term expr_1
           | '-' term expr_1
           |  

term       : unary term_1

term_1     : '*' unary term_1
           | '/' unary term_1
           |  

unary      : '!' unary
           | '-' unary
           | factor


factor     : '(' bool ')'
           | loc
           | num
           | real
           | 'true'
           | 'false'


